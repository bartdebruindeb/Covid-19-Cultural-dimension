
__includes ["cultural_model.nls"]

;;See Maslow Hierarchy of Needs (HON) theory for further details
;;survival needs differ from safety in that they are related to the urgency of physiological risk where safety is rather a matter of general anxiety

;;I set food safety need satisfaction with a "min" operator as it tends to override other needs
;;risk avoidance might be a second option. Though, risk-compliance makes it for a poor proxy; as "safety = risk-avoidance"
;;a more specific concept would be appropriate (e.g. "contagion-safety" for instance, I let you adapt depending on what you want to represent with this variable).

to-report safety-need-satisfaction 
  let psychological-needs (list compliance-need-satisfaction risk-avoidance-need-satisfaction)
  let importances-psychological-needs (list my-importance-for-compliance-needs my-importance-for-risk-avoidance-needs)
  
  ;The young are taken care of by their parents, so do not worry about finances 
  if age != "young" [    
    set psychological-needs fput financial-safety-need-satisfaction psychological-needs
    set importances-psychological-needs fput my-importance-for-financial-safety-needs importances-psychological-needs
  ]
  report weighted-mean psychological-needs importances-psychological-needs
end

to-report self-esteem-need-satisfaction
  let needs (list relaxing-need-satisfaction)
  let importances (list my-importance-for-relaxing-needs)
  
  ;The young people are presumed to not be interested in luxury / outward expressions of wealth.
  if age != "young" [
    set needs fput luxury-need-satisfaction needs
    set importances fput my-importance-for-luxury-needs importances
  ]
  report weighted-mean needs importances
end

;;to be updated later on:
to-report survival-need-satisfaction
  ;;constant value set to average
  ;;survival needs often revolve around short-term temporality (e.g. stopping to breath)
 let physiological-needs (list food-safety-need-satisfaction health-need-satisfaction sleep-need-satisfaction)
  ;The young are taken care of by their parents, so do not worry about finances 
  if age != "young" [ 
    set physiological-needs fput financial-survival-need-satisfaction physiological-needs ]
  
  report min physiological-needs
end


to-report health-need-satisfaction
  if is-believing-to-be-infected?
  [
    if not (is-resting-at-home? or is-currently-allocated-a-bed-in-hospital?) [report 0]
    if is-observing-critical-symptoms? and (is-resting-at-home? or is-currently-allocated-a-bed-in-hospital?) [report 0.1]
    
    if is-resting-at-home? or is-currently-allocated-a-bed-in-hospital? [report 0.3]
    error "undefined"
  ]
  report 1
end

;*** UPDATE(07-04): food safety is considered to be a part of the survival maslow need - this is because it holds a priority position over the psychological needs. ***
;*** food-safety-satisfaction-level ***
;;here, food-safety is rather a subaspect of safety (more cognitive, worries about the future)
;;TBU: should be dependent on the current plan, i.e. acquiring food should be expected to lower this need
to-report food-safety-need-satisfaction
  if is-currently-being-treated-at-hospital? [
    report 1
  ]
  report min (list ([available-food-rations] of my-home / amount-of-rations-I-buy-when-going-to-essential-shops) 1)
end


to-report financial-survival-need-satisfaction
  report clamp 0 1 (my-amount-of-capital / price-of-rations-in-essential-shops * amount-of-rations-I-buy-when-going-to-essential-shops)
end

to-report financial-safety-need-satisfaction
  report ifelse-value my-previous-amount-of-capital > 0 [clamp 0 1 (my-amount-of-capital / my-previous-amount-of-capital)] [0]
end